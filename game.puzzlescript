(
Created for Ludum Dare 33
Casey Brant http://www.twitter.com/basecase
Josh Hertel http://www.twitter.com/herteljt

Ocean Waves Recorded by Mike Koenig, License: Attribution 3.0, http://soundbible.com/1935-Ocean-Waves.html
)

title Delicious Ships!
author Josh & Casey
homepage uhacc.club
require_player_movement
youtube cmiX1FrL_88
(verbose_logging)
run_rules_on_level_start
(realtime_interval 1)
noaction


========
OBJECTS
========

Background
#1072da #177eee
00001
00100
00000
00010
01000

Sidebar
Black

Player
Pink Purple
.....
.000.
00000
01010
.....

PlayerDead
Black White
.....
.000.
01010
00100
01110

GoalShip
Black Red Gray DarkGrey
33333
32233
30000
31113
33333

EmptyGoal
White Black
00000
01110
01110
01110
00000

CompletedGoal
White Green
00000
01110
01110
01110
00000

CargoShip
Black Red Gray
.....
.22..
.0000
.111.
.....

PirateShip
DarkBrown Brown Black
..12.
..1..
..1..
00000
.0000

TentacleCargoShip
Black Red Gray Purple Pink
...3.
.2343
.0300
.131.
.33..

TentaclePirateShip
DarkBrown Brown Black Purple Pink
.312.
.433.
..1.3
00034
.0330

CargoShipwreck
Black Red Gray
...1.
02.2.
.1..0
...0.
0.1..

PirateShipwreck
DarkBrown Brown Black
..0..
1...2
...10
00...
..0..

Island
Yellow Brown #cfc930
00002
00200
20002
00200
20000

Tree
Yellow Brown #05ff1a
00000
02200
02220
00100
01110

Whirlpool1
#0022ff #00e5ff DarkBlue
00000
02110
01210
01120
00000

Whirlpool2
#0022ff #00e5ff DarkBlue
00000
01110
02220
01110
00000

Whirlpool3
#0022ff #00e5ff DarkBlue
00000
01120
01210
02110
00000

Whirlpool4
#0022ff #00e5ff DarkBlue
00000
01210
01210
01210
00000

temp
white
.....
.....
.....
.....
.....


=======
LEGEND
=======

. = Background
# = Sidebar
G = GoalShip
_ = EmptyGoal
@ = Player
P = PirateShip
C = CargoShip
I = Island
T = Tree
W = Whirlpool1

Ship = CargoShip or PirateShip
TentacleShip = TentacleCargoShip or TentaclePirateShip
Whirlpool = Whirlpool1 or Whirlpool2 or Whirlpool3 or Whirlpool4


=======
SOUNDS
=======

PirateShipwreck create 29648102
PlayerDead move 89775108
CargoShipwreck create 46205502
CompletedGoal create 67660100
EndLevel 45420300


================
COLLISIONLAYERS
================

Background
temp
Player, Ship, GoalShip, CompletedGoal, EmptyGoal, TentacleShip, CargoShipwreck, PirateShipwreck, Island, Tree, PlayerDead, GoalShip, Sidebar, Whirlpool


======
RULES
======

late [ Player | CargoShip ] -> [ Player | TentacleCargoShip ]
[ EmptyGoal ] [ > Player | TentacleCargoShip ] -> [ CompletedGoal ] [ > Player | ]
[ < Player | TentacleCargoShip ] -> [ < Player | CargoShipwreck ]
[ TentacleCargoShip | no Player ] -> [ CargoShip | ]

late [ Player | PirateShip ] -> [ Player | TentaclePirateShip ]
[ > Player | TentaclePirateShip ] -> [ > PlayerDead | ]
[ < Player | TentaclePirateShip ] -> [ < Player | PirateShipwreck ]
[ TentaclePirateShip | no Player ] -> [ PirateShip | ]
[ > Player | PirateShipwreck ] -> [ > Player | > PirateShipwreck ]

(((
(Whirlpool Animations)
[stationary Player][Whirlpool1 no temp] -> [stationary Player][Whirlpool2 temp]
[stationary Player][Whirlpool2 no temp] -> [stationary Player][Whirlpool3 temp]
[stationary Player][Whirlpool3 no temp] -> [stationary Player][Whirlpool4 temp]
[stationary Player][Whirlpool4 no temp] -> [stationary Player][Whirlpool1 temp]
[stationary Player][Whirlpool temp] -> [stationary Player][Whirlpool]

(Whirlpool as Player Moves)
[Whirlpool1 no temp] -> [Whirlpool2 temp]
[Whirlpool2 no temp] -> [Whirlpool3 temp]
[Whirlpool3 no temp] -> [Whirlpool4 temp]
[Whirlpool4 no temp] -> [Whirlpool1 temp]
late [Whirlpool temp] -> [Whirlpool]

(Whirlpool Rules)
[ > Player | Whirlpool ] -> [ > PlayerDead | ]
[ < Player | Whirlpool ] -> [ < Player | Whirlpool ]
[ ^ Player | Whirlpool ] -> [ > PlayerDead | ] message You are pulled into the Whirlpool!
[ v Player | Whirlpool ] -> [ > PlayerDead | ] message You are pulled into the Whirlpool!
)))


==============
WINCONDITIONS
==============

No EmptyGoal


=======
LEVELS
=======

message Ah, so hungry! Those cargo ships look amazing!
(Level 1 - introduce goal of eating a set number of cargo ships
	- maybe introduce idea of destroying boats if the player does it)
#........t..
g........ii.
_..@........
_...........
#.......c...
#...........
#...ii......
#..cii......
#...t.......
#...........


(Level 2)
#...........
gciiit......
_.iit.....i.
_.i......ii.
_....@.i.....
#..it..iii..
#.ii.i...ic.
#itc.....t..
#...........
#...........


(Level 3 - introduce idea that destroying boats is sometimes unavoidable but doesn't mean you lose)
#...........
g...........
_......tiiii
_......iccii
#..@.....it.
#.....iicii.
#.....iiii..
#ti.........
#ii.........
#...........


(Level 4 - introduce the mechanic that moving away from a cargo ship creates debris that cannot be moved)
#.........ii
g....t...iii
_...ici...i.
_...i....ii.
_....c..iti.
#..it@iiiiti
#cii.c...i..
#itc........
#.i.......ii
#.......iii.

(Level 5)
#iiiiiiit...
giiitiiii...
_iiiiiiii...
_iiic@cii...
_.ttc.ci....
_...........
#...........
#........ti.
#.........i.
#...........

(Level 6)
#........i..i
g.......tic.i
_..@..i.i..it
_i...itii..ii
_.i.i.iiic..i
#.c.c.i...iii
#.t.i.i.ic...
#ci.i...i....
#..tt...i....
#...i...i....


(Level 7 - you have to be careful about the path you take so you don't lock yourself in with shipwrecks)
#iiiiiii....
giiiiiiiiii.
_...ticccii.
_....ic.ci..
_....ic@ct..
_....ii.ci..
_...........
_...........
_...........
_...........


message Uh oh, those pirates look mean!
(Level 8 - you can push pirate shipwrecks if you hadn't realized it yet)
#itiii......
gitii...@...
_iii........
#iicp.......
#iii........
#ii.........
#i..........
#...........
#...........
#...........


(Level 9 - introduce goal of avoiding pirate ships to get to cargo ships, JOSH TO TWEAK)
#.......ii...
gi...tiiiit..
_i.p.i..iii..
_i.i.@..p...i
_c.tc.....ii.
_..iii.ppct..
#.i.i.i.i....
#ti.iciiii...
#tt....iii...
#t.........t.



(Level 10 - introduce goal of destorying and pushing pirate ships to get to cargo ships)

#....iiit....
giii.tiiiit..
_ii.....iiii.
_i...@...p...
_tti.itiiciii
#...pi.p..t..
#ci.p..ci....
#.iiiiiiiii..
#....iiii....
#.....ti.....


(Level 11 - introduce goal of destorying and pushing pirate ships to get to cargo ships . NEEDS WORK!!!)

#............
gc...tc..ti.
_...iii...i.
_..i@..i.pi.
_i.i...ii...
#..p.p..ic..
#t.i.....i..
#iti.i......
#...........
#...........

message Surrounded!
(Level 12)
#.....i......
gc..i.i..c..
_.i..pip....
_.iii.i.iiii
_..p.@.p.ii.
#itii.ii.ii.
#....p...ii.
#..ci....it.
#...........
#...........



message Yum! Time for a nap.
message Created by basecase and herteljt for Ludum Dare 33
message Thanks for playing!
